buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.h2database:h2:1.4.196"
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
}

apply plugin: 'kotlin'

group 'no.skotbuvel'
version '1.0-SNAPSHOT'

task buildFrontEnd(type: Exec) {
    workingDir 'frontend'
    commandLine 'npm', 'run', 'build'
}

task copyFrontEnd(dependsOn: buildFrontEnd, type: Copy) {
    from 'frontend/build'
    into 'build/resources/main/frontend'
}

flyway {
    url = "jdbc:h2:./db_test"
    user = "sa"
}

task fatJar(dependsOn: copyFrontEnd, type: Jar) {
    manifest {
        attributes "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Main-Class": "no.skotbuvel.portal.ApplicationKt"
    }
    baseName = project.name + "-all"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "com.sparkjava:spark-core:2.6.0"
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "com.google.code.gson:gson:2.8.1"
}
